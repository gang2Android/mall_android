apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.gang.test"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        if (isDebug.toBoolean()) {
            manifestPlaceholders = [firstActivity: ".MainActivity", firstTheme: "@style/AppTheme"]
        } else {
            manifestPlaceholders = [firstActivity: "com.gang.module_main.sp.SplashActivity", firstTheme: "@style/SplashTheme"]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        dataBinding = true
        // for view binding :
        // viewBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$rootProject.ext.kotlin_version"
    implementation "androidx.core:core-ktx:$rootProject.ext.core_ktx"
    implementation "androidx.appcompat:appcompat:$rootProject.ext.appcompat"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.ext.constraintlayout"
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    if (!isDebug.toBoolean()) {
        implementation project(path: ':module_main')
        implementation project(path: ':module_home')
        implementation project(path: ':module_goods')
        implementation project(path: ':module_cart')
        implementation project(path: ':module_user')
        implementation project(path: ':module_order')
    }
    implementation("com.alibaba:arouter-api:$rootProject.ext.arouter_version") {
        exclude module: "fragment"
    }
    kapt "com.alibaba:arouter-compiler:$rootProject.ext.arouter_version"
}
